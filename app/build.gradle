plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
    id "com.mikepenz.aboutlibraries.plugin" version "10.5.2"
}

apply plugin: 'com.mikepenz.aboutlibraries.plugin'

android {
    namespace 'com.starry.myne'
    compileSdk 33

    defaultConfig {
        applicationId "com.starry.myne"
        minSdk 26
        targetSdk 33
        versionCode 14
        versionName "2.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // Rename the output apk file automatically.
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def name = "Myne-v${variant.versionName}.apk"
                    output.outputFileName = name
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs = ['-Xjvm-default=all-compatibility']
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.4'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

aboutLibraries {
    // Remove the "generated" timestamp to allow for reproducible builds
    excludeFields = ["generated"]
}


dependencies {
    // Android core components.
    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.activity:activity-compose:1.7.0'
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.27.0"
    // Jetpack compose.
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.animation:animation:$compose_version"
    implementation "com.google.accompanist:accompanist-navigation-animation:0.29.0-alpha"
    implementation "androidx.compose.material3:material3:1.1.0-beta02"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.6.1"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    // Material theme for main activity.
    implementation 'com.google.android.material:material:1.8.0'
    // Android 12+ splash API.
    implementation 'androidx.core:core-splashscreen:1.0.0'
    // Gson JSON library.
    implementation 'com.google.code.gson:gson:2.10'
    // OkHttp library.
    implementation "com.squareup.okhttp3:okhttp:4.10.0"
    // Coil Image loading library.
    implementation "io.coil-kt:coil-compose:2.2.2"
    // Room database components.
    implementation "androidx.room:room-ktx:2.5.1"
    kapt "androidx.room:room-compiler:2.5.1"
    androidTestImplementation "androidx.room:room-testing:2.5.1"
    // Dagger - Hilt.
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    kapt "androidx.hilt:hilt-compiler:1.0.0"
    // Jsoup HTML Parser.
    implementation "org.jsoup:jsoup:1.15.2"
    // Lottie animations.
    implementation "com.airbnb.android:lottie-compose:4.1.0"
    // DataStore Preferences
    implementation("androidx.datastore:datastore-preferences:1.0.0")
    // Open Source Libraries Screen.
    implementation "com.mikepenz:aboutlibraries-core:10.5.2"
    implementation "com.mikepenz:aboutlibraries-compose:10.5.2"
    // Swipe actions.
    implementation "me.saket.swipe:swipe:1.0.0"
    // Crash Handler.
    implementation 'cat.ereza:customactivityoncrash:2.4.0'
    // Kotlin reflect API.
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.8.10"
    // Testing components.
    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.mockito:mockito-core:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    // Android testing components.
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    // debug components.
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
}
