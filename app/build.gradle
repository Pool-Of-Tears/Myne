plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'dagger.hilt.android.plugin'
    id 'com.google.devtools.ksp'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id "com.mikepenz.aboutlibraries.plugin.android" version "13.1.0"
}

String getGoogleApiKey() {
    def propFile = rootProject.file("./local.properties")
    def properties = new Properties()
    if (propFile.exists()) {
        properties.load(new FileInputStream(propFile))
        return properties.getProperty('GOOGLE_API_KEY', "null")
    } else {
        return "null"
    }
}

android {
    namespace 'com.starry.myne'
    compileSdk = 36

    defaultConfig {
        applicationId "com.starry.myne"
        minSdk 26
        targetSdk 36
        versionCode 461
        versionName "4.6.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        ksp {
            arg('room.schemaLocation', "$projectDir/schemas")
        }

        buildConfigField("String", "GOOGLE_API_KEY", getGoogleApiKey())
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // Rename the output apk file automatically.
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def name = "Myne-v${variant.versionName}.apk"
                    output.outputFileName = name
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    lintOptions {
        abortOnError true
    }
    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs = ['-Xjvm-default=all-compatibility']
    }
    composeCompiler {
        enableStrongSkippingMode = true
    }
    buildFeatures {
        compose true
        buildConfig = true
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    androidResources {
        generateLocaleConfig true
    }

    packaging {
        resources {
            pickFirsts += ['META-INF/versions/9/OSGI-INF/MANIFEST.MF']
        }
    }
}

aboutLibraries {
    export {
        // Remove the "generated" timestamp to allow for reproducible builds
        excludeFields.addAll("generated")
    }
}

dependencies {
    def composeBom = platform('androidx.compose:compose-bom:2025.11.00')
    implementation composeBom
    androidTestImplementation composeBom

    // Android core components.
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.9.4'
    implementation 'androidx.activity:activity-compose:1.11.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.9.4"
    // TODO: Migrate to type-safe navigation.
    implementation "androidx.navigation:navigation-compose:2.9.6"
    // Jetpack compose.
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.compose.material:material"
    implementation "androidx.compose.animation:animation"
    implementation "androidx.compose.runtime:runtime-livedata"
    implementation "androidx.compose.material3:material3"
    // Material icons.
    implementation 'androidx.compose.material:material-icons-extended:1.7.8'
    // Material theme for main activity.
    implementation 'com.google.android.material:material:1.13.0'
    // Android 12+ splash API.
    implementation 'androidx.core:core-splashscreen:1.2.0'
    // KotlinX Serialization library.
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-protobuf:1.9.0"
    // OkHttp library.
    implementation "com.squareup.okhttp3:okhttp:5.3.0"
    implementation "com.squareup.okhttp3:logging-interceptor:5.3.0"
    // Coil Image loading library.
    implementation "io.coil-kt:coil-compose:2.7.0"
    // Room database components.
    //noinspection GradleDependency
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"
    // Dagger - Hilt.
    //noinspection NewerVersionAvailable
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation "androidx.hilt:hilt-navigation-compose:1.3.0"
    ksp "com.google.dagger:hilt-android-compiler:$hilt_version"
    ksp "androidx.hilt:hilt-compiler:1.3.0"
    // Jsoup HTML Parser.
    implementation "org.jsoup:jsoup:1.21.2"
    // Lottie animations.
    implementation "com.airbnb.android:lottie-compose:6.7.1"
    // TapTarget Compose
    implementation "com.pierfrancescosoffritti.taptargetcompose:core:1.2.1"
    // DataStore Preferences.
    implementation("androidx.datastore:datastore-preferences:1.1.7")
    // Open Source Libraries Screen.
    implementation "com.mikepenz:aboutlibraries-core:13.1.0"
    implementation "com.mikepenz:aboutlibraries-compose-m3:13.1.0"
    // Swipe actions.
    implementation "me.saket.swipe:swipe:1.3.0"
    // Crash Handler.
    implementation 'cat.ereza:customactivityoncrash:2.4.0'
    // Kotlin reflect API.
    implementation "org.jetbrains.kotlin:kotlin-reflect:2.2.0"
    // Testing components.
    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.google.truth:truth:1.4.5"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.10.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:6.0.1'
    testImplementation 'org.robolectric:robolectric:4.16'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:6.0.1'
    // Android testing components.
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
    // debug components.
    debugImplementation "androidx.compose.ui:ui-tooling"
    debugImplementation "androidx.compose.ui:ui-test-manifest"
}
